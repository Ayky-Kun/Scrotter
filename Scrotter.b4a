Version=2.52
IconFile=
NumberOfModules=0
Package=com.yttrium.scrotter
DoNotOverwriteManifest=False
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: http://www.basic4ppc.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="14" android:targetSdkVersion="15"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true"~\n~	android:hardwareAccelerated="true"~\n~    android:anyDensity="true"/>)~\n~AddPermission(android.permission.READ_EXTERNAL_STORAGE)~\n~AddPermission(android.permission.WRITE_EXTERNAL_STORAGE)~\n~AddActivityText(Main, ~\n~<intent-filter>~\n~    <action android:name="android.intent.action.SEND" />~\n~	<category android:name="android.intent.category.DEFAULT" />~\n~    <data android:mimeType="image/*" />~\n~</intent-filter>)~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~AddApplicationText(<activity android:name="anywheresoftware.b4a.objects.preferenceactivity"/>)~\n~
UserTypesHint=PanelInfo
NumberOfFiles=4
File1=Main.bal
File2=Options.bal
File3=Preview.bal
File4=Settings.bal
NumberOfLibraries=7
Library1=abextdrawing
Library2=ahviewpager
Library3=core
Library4=phone
Library5=reflection
Library6=sql
Library7=threading
@EndOfDesignText@
#Region  Project Attributes 
	#ApplicationLabel: Scrotter
	#VersionCode: 01
	#VersionName: 0.1
	#SupportedOrientations: portrait
	#CanInstallToExternalStorage: True
#End Region

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: False
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	Dim TYPE_SETTINGS As Int : TYPE_SETTINGS = 1
	Dim TYPE_PREVIEW As Int : TYPE_PREVIEW = 2
	Dim TYPE_OPTIONS As Int : TYPE_OPTIONS = 3
	Dim FILL_PARENT As Int : FILL_PARENT = -1
	Dim WRAP_CONTENT As Int : WRAP_CONTENT = -2
	Type PanelInfo (PanelType As Int, LayoutLoaded As Boolean) 
	Dim CurrentPage As Int : CurrentPage = 0
	Dim version As String = "0.1"
	Dim releasedate As String = "4/18/2013"
	Dim theme As String = "light"
	Dim Loaded(4) As Boolean
End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
	'These variables can only be accessed from this module.
	Dim settingspage As Panel
	Dim optionspage As Panel
	Dim previewpage As Panel
	Dim container As AHPageContainer
	Dim pager As AHViewPager
	Dim tabs As AHViewPagerTabs
	Dim GlossCheckbox As CheckBox
	Dim ModelBox As Spinner
	Dim ShadowCheckbox As CheckBox
	Dim StretchCheckbox As CheckBox
	Dim UnderShadowCheckbox As CheckBox
	Dim VariantBox As Spinner
	Dim TabSwitcher As TabHost
	Dim Loading As ProgressBar
	Dim Loadbtn As Button
	Dim SaveBtn As Button
	Dim Preview As Panel
	Dim LoadedImage As Bitmap
	'Public Shared OpenPath(7), SavePath As String
    'Public OpenStream As Stream = Nothing
    'Public SaveStream As Stream = Nothing
    'Public PhoneStream As Stream = Nothing
    'Public SaveImg As Image
    'Public CanvImg(7) As Image
    'Public Image2 As New Bitmap(720, 1280)
    'Public Shared IsMono As Boolean
    'Public ReadOnly Version As String = "0.8"
    'Public ReadOnly ReleaseDate As String = "2013-02-12"
    'Private Image(7) As String
	Dim CurrentPage As Int
	Dim BackgroundThread As Thread
	Dim PreviewImage As Bitmap
	Dim Waiting As Boolean = False
	Dim cc As ContentChooser
	Dim Ringtone As RingtoneManager
	Dim ScrotterTitle As Label
	Dim IconView As ImageView
	Dim ScrotterVers As Label
	Dim themebtn As Button
	Dim FinalBitmap As Bitmap
End Sub

Sub Activity_Create(FirstTime As Boolean)
	Msgbox("This version of Scrotter is a developer preview only. Many basic functions do not work, and are not yet implemented. Do not treat this version as final!", "Warning")
	container.Initialize
	settingspage = CreatePanel(TYPE_SETTINGS, "Settings")
	container.AddPage(settingspage,"Settings")
	previewpage = CreatePanel(TYPE_PREVIEW, "Preview")
	container.AddPage(previewpage,"Preview")
	optionspage = CreatePanel(TYPE_OPTIONS, "Options")
	container.AddPage(optionspage,"Options")
	pager.Initialize(container, "Pager")
	tabs.Initialize(pager)
	tabs.LineHeight = 5dip
	tabs.UpperCaseTitle = True
	Activity.AddView(tabs, 0, 0, FILL_PARENT, WRAP_CONTENT)
	Activity.AddView(pager, 0, 29dip, Activity.Width, Activity.Height-29dip)
	BackgroundThread.Initialise("ImageThread")
	cc.Initialize("cc")
	tabs.Color = Colors.White
	tabs.BackgroundColorPressed = Colors.DarkGray
	tabs.LineColorCenter = Colors.DarkGray
	tabs.TextColor = Colors.LightGray
	tabs.TextColorCenter = Colors.DarkGray
End Sub

Sub RefreshTheme
	Select theme
		Case "light"
			tabs.Color = Colors.White
			tabs.BackgroundColorPressed = Colors.DarkGray
			tabs.LineColorCenter = Colors.DarkGray
			tabs.TextColor = Colors.LightGray
			tabs.TextColorCenter = Colors.DarkGray
			If Loaded(1) = True Then
				settingspage.Color = Colors.White
				ScrotterTitle.TextColor = Colors.DarkGray
				ScrotterVers.TextColor = Colors.Gray
			End If
			If Loaded(2) = True Then previewpage.Color = Colors.White
			If Loaded(3) = True Then
				optionspage.Color = Colors.White
				ModelBox.TextColor = Colors.DarkGray
				VariantBox.TextColor = Colors.DarkGray
				StretchCheckbox.TextColor = Colors.DarkGray
				GlossCheckbox.TextColor = Colors.DarkGray
				ShadowCheckbox.TextColor = Colors.DarkGray
				UnderShadowCheckbox.TextColor = Colors.DarkGray
			End If
		Case "dark"
			tabs.Color = Colors.RGB(50, 50, 50)
			tabs.BackgroundColorPressed = Colors.White
			tabs.LineColorCenter = Colors.LightGray
			tabs.TextColor = Colors.Gray
			tabs.TextColorCenter = Colors.LightGray
			If Loaded(1) = True Then
				settingspage.Color = Colors.RGB(50, 50, 50)
				ScrotterTitle.TextColor = Colors.LightGray
				ScrotterVers.TextColor = Colors.Gray
			End If
			If Loaded(2) = True Then  previewpage.Color = Colors.RGB(50, 50, 50)
			If Loaded(3) = True Then
				optionspage.Color = Colors.RGB(50, 50, 50)
				ModelBox.TextColor = Colors.LightGray
				VariantBox.TextColor = Colors.LightGray
				StretchCheckbox.TextColor = Colors.LightGray
				GlossCheckbox.TextColor = Colors.LightGray
				ShadowCheckbox.TextColor = Colors.LightGray
				UnderShadowCheckbox.TextColor = Colors.LightGray
			End If
	End Select
	Dim mnum, vnum As Int
	mnum = ModelBox.SelectedIndex
	vnum = VariantBox.SelectedIndex
	ModelBox.SelectedIndex = 0
	ModelBox.SelectedIndex = mnum
	VariantBox.SelectedIndex = 0
	VariantBox.SelectedIndex = vnum
End Sub

Sub CC_Result (Success As Boolean, Dir As String, FileName As String)
	If Success = True Then
		LoadedImage.Initialize3(LoadBitmap(Ringtone.GetContentDir, FileName))
		ImageProcess
	End If
End Sub

Sub Activity_Resume
	pager.GotoPage(CurrentPage, False)
	Dim In As Intent 
    In = Activity.GetStartingIntent 
    
    If In.ExtrasToString.Contains("no extras") Then
               'No Data
    Else
		Log(In.ExtrasToString)
		Dim UriString As String 
		UriString = In.ExtrasToString
		UriString = UriString.SubString2(UriString.IndexOf("STREAM=")+7,UriString.IndexOf("}"))
		If UriString.Contains(",") Then
			UriString = UriString.SubString2(0,UriString.IndexOf(","))
		End If
		Log(UriString)
		LoadedImage.Initialize3(LoadBitmap(Ringtone.GetContentDir, UriString))
		Preview.SetBackgroundImage(ResizeImage(LoadedImage, Preview.Width, Preview.Height))
		pager.GotoPage(1, False)
    End If
End Sub

Sub Activity_Pause (UserClosed As Boolean)
	CurrentPage = pager.CurrentPage
End Sub

Sub CreatePanel(PanelType As Int, Title As String) As Panel
	Dim pan As Panel
	Dim pi As PanelInfo
	pi.Initialize
	pi.LayoutLoaded = False
	pi.PanelType = PanelType
	pan.Initialize("")
	pan.Tag = pi
	Return pan
End Sub

Sub Pager_PageCreated (Position As Int, Page As Object)
	Log ("Page created " & Position)
	Dim pan As Panel
	Dim pi As PanelInfo
	pan = Page
	pi = pan.Tag
	Select pi.PanelType
		Case TYPE_SETTINGS
			If Not(pi.LayoutLoaded) Then
				pan.LoadLayout("Settings")
				pi.LayoutLoaded = True
				ScrotterTitle.Text = "Scrotter"
				ScrotterTitle.TextSize = ScrotterTitle.Height * 800/1000dip
				ScrotterVers.Text = "v" & version & " (" & releasedate & ")"
				ScrotterVers.TextSize = ScrotterVers.Height * 500/1000dip
				Select theme
					Case "light"
						settingspage.Color = Colors.White
						ScrotterTitle.TextColor = Colors.DarkGray
						ScrotterVers.TextColor = Colors.Gray
					Case "dark"
						settingspage.Color = Colors.RGB(50, 50, 50)
						ScrotterTitle.TextColor = Colors.LightGray
						ScrotterVers.TextColor = Colors.Gray
				End Select
			End If
			Loaded(1) = True
		Case TYPE_PREVIEW
			If Not(pi.LayoutLoaded) Then
				pan.LoadLayout("Preview")
				pi.LayoutLoaded = True
				Select theme
					Case "light"
						previewpage.Color = Colors.White
					Case "dark"
						previewpage.Color = Colors.RGB(50, 50, 50)
				End Select
			End If
			Loaded(2) = True
		Case TYPE_OPTIONS
			If Not(pi.LayoutLoaded) Then
				pan.LoadLayout("Options")
				pi.LayoutLoaded = True
				ModelBox.AddAll(Array As String("Google Nexus 4", "Google Nexus 7", "Google Nexus S", "HTC Desire HD, HTC Inspire 4G", "HTC One S", "HTC One V", "HTC One X, HTC One X+", "Motorola Droid RAZR", "Motorola Droid RAZR M", "Motorola Xoom", "Samsung Galaxy Note II", "Samsung Galaxy SII, Epic 4G Touch", "Samsung Galaxy SIII", "Samsung Galaxy SIII Mini", "Samsung Google Galaxy Nexus"))
				Select theme
					Case "light"
						optionspage.Color = Colors.White
						ModelBox.TextColor = Colors.DarkGray
						VariantBox.TextColor = Colors.DarkGray
						StretchCheckbox.TextColor = Colors.DarkGray
						GlossCheckbox.TextColor = Colors.DarkGray
						ShadowCheckbox.TextColor = Colors.DarkGray
						UnderShadowCheckbox.TextColor = Colors.DarkGray
					Case "dark"
						optionspage.Color = Colors.RGB(50, 50, 50)
						ModelBox.TextColor = Colors.LightGray
						VariantBox.TextColor = Colors.LightGray
						StretchCheckbox.TextColor = Colors.LightGray
						GlossCheckbox.TextColor = Colors.LightGray
						ShadowCheckbox.TextColor = Colors.LightGray
						UnderShadowCheckbox.TextColor = Colors.LightGray
				End Select
				ModelBox.Invalidate
				VariantBox.Invalidate
			End If
			Loaded(3) = True
	End Select		
End Sub

Sub Pager_PageChanged (Position As Int)
	CurrentPage = Position
End Sub

Sub ModelBox_itemClick (Position As Int, Value As Object)
	VariantBox.Clear
	Select Case ModelBox.SelectedItem
		Case "HTC One X, HTC One X+"
			VariantBox.AddAll(Array As String("White", "Black"))
		Case "Samsung Galaxy SIII"
			VariantBox.AddAll(Array As String("Blue", "White", "Black", "Red", "Brown"))
		Case "Motorola Xoom"
			VariantBox.AddAll(Array As String("Landscape", "Portrait"))
		Case "Samsung Galaxy SII, Epic 4G Touch"
			VariantBox.AddAll(Array As String("Galaxy SII", "Epic 4G Touch"))
	End Select
	Loading.Visible = True
	If BackgroundThread.Running = True Then
		BackgroundThread.Interrupt
	End If
	BackgroundThread.Start(Me, "ImageProcess", Null)
	pager.PagingEnabled = False
	Loadbtn.Enabled = True
	SaveBtn.Enabled = True
End Sub

Sub SaveBtn_Click
	DateTime.DateFormat = "yyyyMMdd_HHmmss"
	Dim result As Int
	result = Msgbox2("Save file as what format?", "Save Image", "PNG", "Cancel", "JPG", Null)
	Select Case result
		Case DialogResponse.POSITIVE
			Dim filename As String = "Scrotter4A_" & DateTime.Date(DateTime.now) & ".png"
			Dim Out As OutputStream
			Out = File.OpenOutput(File.Combine(File.DirRootExternal, "Scrotter/"), filename, False)
			FinalBitmap.WriteToStream(Out, 100, "PNG")
			Out.Flush
			Out.Close
			ToastMessageShow ("File saved to the sdcard at /sdcard/Scrotter/" & filename & ".", True)
		Case DialogResponse.NEGATIVE
			Dim filename As String = "Scrotter4A_" & DateTime.Date(DateTime.now) & ".jpg"
			Dim Out As OutputStream
			Out = File.OpenOutput(File.Combine(File.DirRootExternal, "Scrotter/"), filename, False)
			FinalBitmap.WriteToStream(Out, 95, "JPEG")
			Out.Flush
			Out.Close
			ToastMessageShow ("File saved to the sdcard at /sdcard/Scrotter/" & filename & ".", True)
	End Select
End Sub

Sub Loadbtn_Click
	Try
		cc.Show("image/*", "")
	Catch
	End Try
End Sub

Sub ImageProcess
	Dim Device As Bitmap
	Dim WorkingCanvas As Canvas
	Dim WorkingBitmap As Bitmap
	Dim Gloss As Bitmap
	Dim Shadow As Bitmap
	Dim Undershadow As Bitmap
	Dim IndexH As Int
	Dim IndexW As Int
	Dim ExtDraw As ABExtDrawing
	Dim Paint As ABPaint
	Dim r480800 As String = "480x800.png"
	Dim r540960 As String = "540x960.png"
	Dim r7201280 As String = "720x1280.png"
	Dim r7681280 As String = "768x1280.png"
	Dim r8001280 As String = "800x1280.png"
	Dim r1280800 As String = "1280x800.png"
	'end of start inits
	Select Case ModelBox.SelectedItem
		Case "Samsung Galaxy SIII Mini"
                    Device.Initialize(File.DirAssets, "device/" & "samsunggsiiimini.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r480800)
                    IndexW = 78
                    IndexH = 182
        Case "HTC Desire HD, HTC Inspire 4G"
                    Device.Initialize(File.DirAssets, "device/" & "desirehd.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r480800)
                    IndexW = 86
                    IndexH = 130
        Case "HTC One X, HTC One X+"
                    If VariantBox.SelectedItem = "Black" Then
                        Device.Initialize(File.DirAssets, "device/" & "onexblack.png")
                        Gloss.Initialize(File.DirAssets, "gloss/" & "onexblack.png")
                        IndexW = 113
                    Else If VariantBox.SelectedItem = "White" Then
                        Device.Initialize(File.DirAssets, "device/" & "onexwhite.png")
                        Gloss.Initialize(File.DirAssets, "gloss/" & "onexwhite.png")
                        IndexW = 115
                    End If
                    Shadow.Initialize(File.DirAssets, "shadow/" & r7201280)
                    IndexH = 213
    	Case "Samsung Galaxy SIII"
                    IndexW = 88
                    If VariantBox.SelectedItem = "Blue" Then
                        Device.Initialize(File.DirAssets, "device/" & "gsiiiblue.png")
                        Gloss.Initialize(File.DirAssets, "gloss/" & "gsiiiblue.png")
                    Else If VariantBox.SelectedItem = "White" Then
                        Device.Initialize(File.DirAssets, "device/" & "gsiiiwhite.png")
                        Gloss.Initialize(File.DirAssets, "gloss/" & "gsiiiwhite.png")
                        IndexW = 84
                    Else If VariantBox.SelectedItem = "Black" Then
                        Device.Initialize(File.DirAssets, "device/" & "gsiiiblack.png")
                    Else If VariantBox.SelectedItem = "Red" Then
                        Device.Initialize(File.DirAssets, "device/" & "gsiiired.png")
                    Else If VariantBox.SelectedItem = "Brown" Then
                        Device.Initialize(File.DirAssets, "device/" & "gsiiibrown.png")
                    End If
                    Undershadow.Initialize(File.DirAssets, "undershadow/" & "gsiii.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r7201280)
                    IndexH = 184
                Case "Motorola Xoom"
                    If VariantBox.SelectedItem = "Portrait" Then
                        Device.Initialize(File.DirAssets, "device/" & "xoomport.png")
                        Shadow.Initialize(File.DirAssets, "shadow/" & r8001280)
                        Gloss.Initialize(File.DirAssets, "gloss/" & "xoomport.png")
                        Undershadow.Initialize(File.DirAssets, "undershadow/" & "xoomport.png")
                        IndexW = 199
                        IndexH = 200
                    Else If VariantBox.SelectedItem = "Landscape" Then
                        Device.Initialize(File.DirAssets, "device/" & "xoomland.png")
                        Shadow.Initialize(File.DirAssets, "shadow/" & r1280800)
                        Gloss.Initialize(File.DirAssets, "gloss/" & "xoomland.png")
                        Undershadow.Initialize(File.DirAssets, "undershadow/" & "xoomland.png")
                        IndexW = 218
                        IndexH = 191
                    End If
                Case "Samsung Galaxy SII, Epic 4G Touch"
                    If VariantBox.SelectedItem = "Galaxy SII" Then
                        Device.Initialize(File.DirAssets, "device/" & "gsii.png")
                        Gloss.Initialize(File.DirAssets, "gloss/" & "gsii.png")
                        Undershadow.Initialize(File.DirAssets, "undershadow/" & "gsii.png")
                        IndexH = 191
                    Else If VariantBox.SelectedItem = "Epic 4G Touch" Then
                        Device.Initialize(File.DirAssets, "device/" & "epic4gtouch.png")
                        Gloss.Initialize(File.DirAssets, "gloss/" & "epic4gtouch.png")
                        Undershadow.Initialize(File.DirAssets, "undershadow/" & "epic4gtouch")
                    End If
                    Shadow.Initialize(File.DirAssets, "shadow/" & r480800)
                    IndexW = 132
                Case "Samsung Google Galaxy Nexus"
                    Device.Initialize(File.DirAssets, "device/" & "galaxynexus.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r7201280)
                    Gloss.Initialize(File.DirAssets, "gloss/" & "galaxynexus.png")
                    Undershadow.Initialize(File.DirAssets, "undershadow/" & "galaxynexus.png")
                    IndexW = 155
                    IndexH = 263
                Case "Samsung Galaxy Note II"
                    Device.Initialize(File.DirAssets, "device/" & "galaxynoteii.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r7201280)
                    Gloss.Initialize(File.DirAssets, "gloss/" & "galaxynoteii.png")
                    IndexW = 49
                    IndexH = 140
                Case "Motorola Droid RAZR"
                    Device.Initialize(File.DirAssets, "device/" & "droidrazr.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r540960)
                    IndexW = 150
                    IndexH = 206
                Case "Google Nexus 7"
                    If VariantBox.SelectedItem = "Portrait" Then
                        Device.Initialize(File.DirAssets, "device/" & "nexus7port.png")
                        Shadow.Initialize(File.DirAssets, "shadow/" & r8001280)
                        Gloss.Initialize(File.DirAssets, "gloss/" & "nexus7port.png")
                        Undershadow.Initialize(File.DirAssets, "undershadow/" & "nexus7port.png")
                        IndexW = 264
                        IndexH = 311
                    Else If VariantBox.SelectedItem = "Landscape" Then
                        Device.Initialize(File.DirAssets, "device/" & "nexus7land.png")
                        Shadow.Initialize(File.DirAssets, "shadow/" & r1280800)
                        Gloss.Initialize(File.DirAssets, "gloss/" & "nexus7land.png")
                        IndexW = 315
                        IndexH = 270
                    End If
                Case "HTC One S"
                    Device.Initialize(File.DirAssets, "device/" & "ones.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r540960)
                    Gloss.Initialize(File.DirAssets, "gloss/" & "ones.png")
                    IndexW = 106
                    IndexH = 228
                Case "HTC One V"
                    Device.Initialize(File.DirAssets, "device/" & "onev.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r480800)
                    Gloss.Initialize(File.DirAssets, "gloss/" & "onev.png")
                    IndexW = 85
                    IndexH = 165
                Case "Google Nexus S"
                    Device.Initialize(File.DirAssets, "device/" & "nexuss.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r480800)
                    Gloss.Initialize(File.DirAssets, "gloss/" & "nexuss.png")
                    IndexW = 45
                    IndexH = 165
                Case "Google Nexus 4"
                    Device.Initialize(File.DirAssets, "device/" & "nexus4.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r7681280)
                    Gloss.Initialize(File.DirAssets, "gloss/" & "nexus4.png")
                    IndexW = 45
                    IndexH = 193
                Case "Motorola Droid RAZR M"
                    Device.Initialize(File.DirAssets, "device/" & "droidrazrm.png")
                    Shadow.Initialize(File.DirAssets, "shadow/" & r540960)
                    IndexW = 49
                    IndexH = 129
            End Select
	Dim r As Rect
	r.Initialize(0, 0, Device.Width, Device.Height)
	WorkingBitmap.InitializeMutable(Device.Width, Device.Height)
	WorkingCanvas.Initialize2(WorkingBitmap)
	Paint.Initialize()
	If Undershadow.IsInitialized Then ExtDraw.drawBitmap(WorkingCanvas, Undershadow, Null, r, Paint)
	Dim r2 As Rect
	r2.Initialize(IndexW, IndexH, IndexW + Shadow.Width, IndexH + Shadow.Height)
	If LoadedImage.IsInitialized Then ExtDraw.drawBitmap(WorkingCanvas, LoadedImage, Null, r2, Paint)
	If Shadow.IsInitialized Then ExtDraw.drawBitmap(WorkingCanvas, Shadow, Null, r2, Paint)
	If Device.IsInitialized Then ExtDraw.drawBitmap(WorkingCanvas, Device, Null, r, Paint)
	If Gloss.IsInitialized Then ExtDraw.drawBitmap(WorkingCanvas, Gloss, Null, r, Paint) 'end of drawing
	PreviewImage.Initialize3(ResizeImage(WorkingBitmap, Preview.Width, Preview.Height))
	FinalBitmap.Initialize3(WorkingBitmap)
	BackgroundThread.RunOnGuiThread("EndLoading", Null)
End Sub

Sub EndLoading
	Loading.Visible = False
	Preview.SetBackgroundImage(PreviewImage)
	pager.PagingEnabled = True
End Sub

Sub ResizeImage(original As Bitmap, TargetX As Int, TargetY As Int) As Bitmap
    Dim origRatio As Float = original.Width / original.Height
    Dim targetRatio As Float = TargetX / TargetY
    Dim scale As Float
    If targetRatio > origRatio Then
        scale = TargetY / original.Height
    Else
        scale = TargetX / original.Width
    End If
    Dim C As Canvas
    Dim b As Bitmap
    b.InitializeMutable(TargetX, TargetY)
    C.Initialize2(b)
    C.DrawColor(Colors.Transparent)
    Dim R As Rect
    Dim w = original.Width * scale, h = original.Height * scale As Int
    R.Initialize(TargetX/2-w/2, TargetY/2-h/2, TargetX/2+w/2, TargetY/2+h/2)
	Dim ExtDraw As ABExtDrawing
	Dim paint As ABPaint
	paint.Initialize()
	paint.setFilterBitmap(True)
	paint.SetAntiAlias(True)
	ExtDraw.drawBitmap(C, original, Null, R, paint)
	Return b
End Sub

Sub VariantBox_ItemClick (Position As Int, Value As Object)
	Loading.Visible = True
	If BackgroundThread.Running = True Then
		BackgroundThread.Interrupt
	End If
	BackgroundThread.Start(Me, "ImageProcess", Null)
	pager.PagingEnabled = False
End Sub

Sub themebtn_Click
	If theme = "light" Then
		theme = "dark"
	Else
		theme = "light"
	End If
	RefreshTheme
End Sub

Sub activity_KeyPress (KeyCode As Int) As Boolean
	If KeyCode = KeyCodes.KEYCODE_BACK Then
		If (pager.CurrentPage = 1) = False AND pager.PagingEnabled = True Then
			pager.GotoPage(1, True)
			Return True
		End If
	End If
End Sub